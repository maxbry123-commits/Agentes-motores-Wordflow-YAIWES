defmodule Jidoka.ParallelToolCallingIntegrationTest do
  use ExUnit.Case, async: true

  alias Jidoka.Agent
  alias Jidoka.Adapter.Runic.OperationBatch
  alias Jidoka.Effect
  alias Jidoka.Review
  alias Jidoka.Snapshot
  alias Jidoka.Runtime.LocalOperations
  alias Jidoka.Runtime.Capabilities
  alias Jidoka.Turn

  import Jidoka.TestSupport, only: [count_results: 2, timeline: 1]

  test "one model turn can execute independent operation calls in parallel" do
    test_pid = self()

    task =
      Task.async(fn ->
        Jidoka.turn(spec(["slow_a", "slow_b"]), "Run both lookups.",
          llm: batched_llm(["slow_a", "slow_b"], "Both lookups finished."),
          operations: blocking_operations(test_pid, ["slow_a", "slow_b"]),
          max_parallel_operations: 2
        )
      end)

    assert_receive {:operation_started, "slow_a", slow_a_pid}, 1_000
    assert_receive {:operation_started, "slow_b", slow_b_pid}, 1_000

    send(slow_b_pid, {:release_operation, "slow_b"})
    send(slow_a_pid, {:release_operation, "slow_a"})

    assert {:ok, %Turn.Result{content: "Both lookups finished."} = result} =
             Task.await(task, 2_000)

    assert [
             %Effect.OperationResult{operation: "slow_a", output: %{"operation" => "slow_a"}},
             %Effect.OperationResult{operation: "slow_b", output: %{"operation" => "slow_b"}}
           ] = result.agent_state.operation_results

    assert [user, assistant_calls, first_tool, second_tool, final] = result.agent_state.messages
    assert user.role == :user
    assert assistant_calls.role == :assistant
    assert first_tool.role == :tool
    assert second_tool.role == :tool
    assert final.role == :assistant

    calls = hd(assistant_calls.interaction.tool_call_groups).calls
    assert calls == [first_tool.tool_call, second_tool.tool_call]
    assert Enum.map(calls, & &1.call_index) == [0, 1]
    assert length(Enum.uniq(Enum.map(result.agent_state.messages, & &1.id))) == 5

    events = timeline(result.events)

    first_completed =
      min(
        operation_event_index(events, :capability_call_completed, "slow_a"),
        operation_event_index(events, :capability_call_completed, "slow_b")
      )

    assert operation_event_index(events, :capability_call_started, "slow_a") < first_completed
    assert operation_event_index(events, :capability_call_started, "slow_b") < first_completed
  end

  test "operation batches use the configured default parallelism" do
    test_pid = self()
    operation_names = Enum.map(1..8, &"slow_#{&1}")

    task =
      Task.async(fn ->
        Jidoka.turn(spec(operation_names), "Run all lookups.",
          llm: batched_llm(operation_names, "All lookups finished."),
          operations: blocking_operations(test_pid, operation_names)
        )
      end)

    started =
      Enum.map(operation_names, fn name ->
        assert_receive {:operation_started, ^name, pid}, 1_000
        {name, pid}
      end)

    Enum.each(started, fn {name, pid} ->
      send(pid, {:release_operation, name})
    end)

    assert {:ok, %Turn.Result{content: "All lookups finished."} = result} =
             Task.await(task, 5_000)

    assert Enum.map(result.agent_state.operation_results, & &1.operation) == operation_names
  end

  test "operation batch capability timeouts run once and leave no orphan task" do
    test_pid = self()
    {:ok, calls} = Elixir.Agent.start_link(fn -> 0 end)
    intents = [operation_intent("fast"), operation_intent("hung")]
    state = operation_batch_state(intents)

    operations =
      LocalOperations.operations(%{
        "fast" => fn _arguments, _context -> {:ok, %{value: "fast"}} end,
        "hung" => fn _arguments, _context ->
          Elixir.Agent.update(calls, &(&1 + 1))
          send(test_pid, {:hung_operation_started, self()})
          Process.sleep(5_000)
          {:ok, %{value: "late"}}
        end
      })

    capabilities = Capabilities.new!(llm: missing_llm(), operations: operations)

    assert {:ok, results} =
             OperationBatch.execute(state, intents, capabilities, state.journal,
               capability_timeout_ms: 10,
               max_parallel_operations: 2,
               operation_retry: [max_attempts: 1]
             )

    assert_receive {:hung_operation_started, hung_pid}, 1_000

    assert [
             %Effect.Result{status: :ok, output: %{value: "fast"}},
             %Effect.Result{
               status: :error,
               output: %Jidoka.Error.ExecutionError{details: %{reason: :capability_timeout}}
             }
           ] = Enum.map(intents, &Map.fetch!(results, &1.id))

    assert Elixir.Agent.get(calls, & &1) == 1
    refute Process.alive?(hung_pid)
  end

  test "operation batches keep the turn-wide deadline" do
    test_pid = self()
    intent = operation_intent("deadline")
    %Turn.State{} = initial_state = operation_batch_state([intent])
    %Turn.Plan{} = initial_plan = initial_state.plan

    state = %Turn.State{
      initial_state
      | plan: %Turn.Plan{initial_plan | timeout_ms: 5},
        started_at_ms: 0
    }

    operations =
      LocalOperations.operations(%{
        "deadline" => fn _arguments, _context ->
          send(test_pid, {:deadline_operation_started, self()})
          Process.sleep(5_000)
          {:ok, %{value: "late"}}
        end
      })

    capabilities = Capabilities.new!(llm: missing_llm(), operations: operations)
    intent_id = intent.id

    assert {:ok, %{^intent_id => %Effect.Result{status: :error}}} =
             OperationBatch.execute(state, [intent], capabilities, state.journal,
               capability_timeout_ms: :infinity,
               clock: fn -> 4 end
             )

    assert_receive {:deadline_operation_started, operation_pid}, 1_000
    refute Process.alive?(operation_pid)
  end

  test "a preflighted group can run serially and keeps observation order" do
    test_pid = self()
    operation_names = ["serial_a", "serial_b", "serial_c"]

    task =
      Task.async(fn ->
        Jidoka.turn(spec(operation_names), "Run the serial group.",
          llm: batched_llm(operation_names, "Serial group finished."),
          operations: blocking_operations(test_pid, operation_names),
          max_parallel_operations: 1
        )
      end)

    started_names =
      Enum.map(operation_names, fn _operation ->
        assert_receive {:operation_started, name, operation_pid}, 1_000
        assert name in operation_names
        refute_receive {:operation_started, _other_name, _other_pid}, 25
        send(operation_pid, {:release_operation, name})
        name
      end)

    assert {:ok, %Turn.Result{} = result} = Task.await(task, 2_000)
    assert Enum.sort(started_names) == Enum.sort(operation_names)
    assert Enum.map(result.agent_state.operation_results, & &1.operation) == operation_names
  end

  test "duplicate operation calls in one batch are both executed and observed" do
    test_pid = self()

    operations =
      LocalOperations.operations(%{
        "lookup" => fn %{"id" => id}, _ctx ->
          send(test_pid, {:lookup_called, id})
          {:ok, %{"id" => id}}
        end
      })

    assert {:ok, %Turn.Result{} = result} =
             Jidoka.turn(spec(["lookup"]), "Look up both ids.",
               llm:
                 batched_llm(
                   [
                     {"lookup", %{"id" => "A"}},
                     {"lookup", %{"id" => "A"}}
                   ],
                   "Both duplicate lookups finished."
                 ),
               operations: operations,
               max_parallel_operations: 2
             )

    assert [
             %Effect.OperationResult{operation: "lookup", effect_id: first_effect_id},
             %Effect.OperationResult{operation: "lookup", effect_id: second_effect_id}
           ] = result.agent_state.operation_results

    assert first_effect_id != second_effect_id
    assert_receive {:lookup_called, "A"}, 1_000
    assert_receive {:lookup_called, "A"}, 1_000
  end

  test "a review interrupt in a batch hibernates before any operation executes" do
    test_pid = self()

    request =
      Turn.Request.new!(
        input: "Run safe lookup and reviewed lookup.",
        metadata: %{
          test_pid: test_pid,
          operation_control_decision: {:interrupt, :approval_required}
        }
      )

    assert {:hibernate, %Snapshot{} = snapshot} =
             Jidoka.turn(review_spec(), request,
               llm: batched_llm(["safe_lookup", "review_lookup"], "Reviewed batch finished."),
               operations: observed_operations(test_pid, ["safe_lookup", "review_lookup"]),
               clock: clock(1_000)
             )

    assert snapshot.cursor.phase == :review
    assert snapshot.turn_state.pending_interrupt.operation == "review_lookup"
    assert length(snapshot.turn_state.pending_effects) == 2

    refute_received {:operation_called, "safe_lookup"}
    refute_received {:operation_called, "review_lookup"}

    events = timeline(snapshot.turn_state.events)
    refute Enum.any?(events, &match?(%{event: :capability_call_started, effect_kind: :operation}, &1))
    assert Enum.any?(events, &match?(%{event: :control_interrupted, operation: "review_lookup"}, &1))

    approval =
      snapshot.turn_state.pending_interrupt
      |> Review.Response.approve(responded_at_ms: 1_001)

    assert {:ok, %Turn.Result{content: "Reviewed batch finished."}} =
             Jidoka.resume(snapshot,
               approval: approval,
               llm: batched_llm(["safe_lookup", "review_lookup"], "Reviewed batch finished."),
               operations: observed_operations(test_pid, ["safe_lookup", "review_lookup"]),
               clock: clock(1_001)
             )

    assert_receive {:operation_called, "safe_lookup"}, 1_000
    assert_receive {:operation_called, "review_lookup"}, 1_000
  end

  test "before-effect checkpoints keep the complete batch behind review" do
    test_pid = self()

    request =
      Turn.Request.new!(
        input: "Run safe lookup and reviewed lookup.",
        metadata: %{
          test_pid: test_pid,
          operation_control_decision: {:interrupt, :approval_required}
        }
      )

    opts = [
      llm: batched_llm(["safe_lookup", "review_lookup"], "Reviewed batch finished."),
      operations: observed_operations(test_pid, ["safe_lookup", "review_lookup"]),
      checkpoint: :before_each_effect,
      clock: clock(2_000)
    ]

    assert {:hibernate, %Snapshot{} = model_checkpoint} =
             Jidoka.turn(review_spec(), request, opts)

    assert model_checkpoint.cursor.phase == :before_effect
    assert Turn.State.current_pending_effect(model_checkpoint.turn_state).kind == :llm
    refute_received {:operation_called, "safe_lookup"}
    refute_received {:operation_called, "review_lookup"}

    assert {:hibernate, %Snapshot{} = operation_checkpoint} =
             Jidoka.resume(model_checkpoint, opts)

    assert operation_checkpoint.cursor.phase == :before_effect
    assert Turn.State.current_pending_effect(operation_checkpoint.turn_state).kind == :operation
    refute_received {:operation_called, "safe_lookup"}
    refute_received {:operation_called, "review_lookup"}

    assert {:hibernate, %Snapshot{} = review_snapshot} =
             Jidoka.resume(operation_checkpoint, opts)

    assert review_snapshot.cursor.phase == :review
    assert review_snapshot.turn_state.pending_interrupt.operation == "review_lookup"
    refute_received {:operation_called, "safe_lookup"}
    refute_received {:operation_called, "review_lookup"}

    approval =
      review_snapshot.turn_state.pending_interrupt
      |> Review.Response.approve(responded_at_ms: 2_001)

    assert {:hibernate, %Snapshot{} = final_model_checkpoint} =
             Jidoka.resume(review_snapshot, Keyword.put(opts, :approval, approval))

    assert_receive {:operation_called, "safe_lookup"}, 1_000
    assert_receive {:operation_called, "review_lookup"}, 1_000

    assert Enum.map(final_model_checkpoint.turn_state.agent_state.operation_results, & &1.operation) == [
             "safe_lookup",
             "review_lookup"
           ]

    assert {:ok, %Turn.Result{content: "Reviewed batch finished."}} =
             Jidoka.resume(final_model_checkpoint, opts)
  end

  test "before-effect checkpoints preflight every group policy before scheduling" do
    test_pid = self()

    policy = fn request, _context ->
      send(test_pid, {:policy_checked, request.action})

      if request.action == "denied_lookup" do
        {:ok,
         Jidoka.Policy.Decision.new!(
           outcome: :deny,
           rule_id: "test.deny",
           reason: :not_allowed
         )}
      else
        {:ok, Jidoka.Policy.Decision.new!(outcome: :allow, rule_id: "test.allow")}
      end
    end

    opts = [
      llm: batched_llm(["safe_lookup", "denied_lookup"], "Unreachable."),
      operations: observed_operations(test_pid, ["safe_lookup", "denied_lookup"]),
      policy: policy,
      checkpoint: :before_each_effect
    ]

    assert {:hibernate, %Snapshot{} = model_checkpoint} =
             Jidoka.turn(spec(["safe_lookup", "denied_lookup"]), "Run both lookups.", opts)

    assert {:hibernate, %Snapshot{} = operation_checkpoint} =
             Jidoka.resume(model_checkpoint, opts)

    assert {:error, %Jidoka.Error.ExecutionError{}} =
             Jidoka.resume(operation_checkpoint, opts)

    assert_receive {:policy_checked, "safe_lookup"}
    assert_receive {:policy_checked, "denied_lookup"}
    refute_received {:operation_called, "safe_lookup"}
    refute_received {:operation_called, "denied_lookup"}
  end

  test "after-phase checkpoints keep the complete batch behind review" do
    test_pid = self()

    request =
      Turn.Request.new!(
        input: "Run safe lookup and reviewed lookup.",
        metadata: %{
          test_pid: test_pid,
          operation_control_decision: {:interrupt, :approval_required}
        }
      )

    opts = [
      llm: batched_llm(["safe_lookup", "review_lookup"], "Reviewed batch finished."),
      operations: observed_operations(test_pid, ["safe_lookup", "review_lookup"]),
      checkpoint: :after_each_phase,
      clock: clock(3_000)
    ]

    assert {:hibernate, %Snapshot{} = phase_checkpoint} =
             Jidoka.turn(review_spec(), request, opts)

    assert phase_checkpoint.cursor.phase == :after_prompt
    assert Turn.State.current_pending_effect(phase_checkpoint.turn_state).kind == :llm

    assert {:hibernate, %Snapshot{} = operation_checkpoint} =
             Jidoka.resume(phase_checkpoint, opts)

    assert operation_checkpoint.cursor.phase == :before_effect
    assert Turn.State.current_pending_effect(operation_checkpoint.turn_state).kind == :operation
    refute_received {:operation_called, "safe_lookup"}
    refute_received {:operation_called, "review_lookup"}

    assert {:hibernate, %Snapshot{} = review_snapshot} =
             Jidoka.resume(operation_checkpoint, opts)

    assert review_snapshot.cursor.phase == :review
    assert review_snapshot.turn_state.pending_interrupt.operation == "review_lookup"
    refute_received {:operation_called, "safe_lookup"}
    refute_received {:operation_called, "review_lookup"}
  end

  defp spec(operation_names) do
    Agent.Spec.new!(
      id: "parallel_tool_calling_agent",
      instructions: "Use available operations before answering.",
      model: %{provider: :test, id: "model"},
      operations: Enum.map(operation_names, &Agent.Spec.Operation.new!(name: &1)),
      runtime_defaults: %{max_model_turns: 4}
    )
  end

  defp review_spec do
    Agent.Spec.new!(
      id: "parallel_review_agent",
      instructions: "Use available operations before answering.",
      model: %{provider: :test, id: "model"},
      operations: [
        Agent.Spec.Operation.new!(name: "safe_lookup"),
        Agent.Spec.Operation.new!(name: "review_lookup")
      ],
      controls: %{
        operations: [
          %{
            control: Jidoka.IntegrationSupport.OperationDecisionControl,
            match: %{name: "review_lookup"}
          }
        ]
      },
      runtime_defaults: %{max_model_turns: 4}
    )
  end

  defp batched_llm(operations, final_content) do
    fn _intent, %Effect.Journal{} = journal, _ctx ->
      case count_results(journal, :operation) do
        0 -> {:ok, %{type: :operations, operations: operation_decisions(operations)}}
        _count -> {:ok, %{type: :final, content: final_content}}
      end
    end
  end

  defp operation_decisions(operations) do
    Enum.map(operations, fn
      {name, arguments} -> %{name: name, arguments: arguments}
      name when is_binary(name) -> %{name: name, arguments: %{}}
    end)
  end

  defp operation_intent(name), do: Effect.Intent.new(:operation, %{name: name, arguments: %{}})

  defp operation_batch_state(intents) do
    spec = spec(Enum.map(intents, & &1.payload.name))
    request = Turn.Request.new!(input: "Run the batch.")

    Turn.State.new!(
      spec: spec,
      plan: Turn.Plan.new!(spec),
      request: request,
      agent_state: request.agent_state
    )
  end

  defp missing_llm, do: fn _intent, _journal, _context -> {:error, :missing_llm} end

  defp blocking_operations(test_pid, operation_names) do
    handlers =
      Map.new(operation_names, fn name ->
        {name,
         fn _arguments, _ctx ->
           send(test_pid, {:operation_started, name, self()})

           receive do
             {:release_operation, ^name} -> {:ok, %{"operation" => name}}
           after
             1_000 -> {:error, {:operation_not_released, name}}
           end
         end}
      end)

    LocalOperations.operations(handlers)
  end

  defp observed_operations(test_pid, operation_names) do
    handlers =
      Map.new(operation_names, fn name ->
        {name,
         fn _arguments, _ctx ->
           send(test_pid, {:operation_called, name})
           {:ok, %{"operation" => name}}
         end}
      end)

    LocalOperations.operations(handlers)
  end

  defp operation_event_index(events, event, operation) do
    Enum.find_index(events, &match?(%{event: ^event, operation: ^operation}, &1))
  end

  defp clock(now_ms), do: fn -> now_ms end
end
