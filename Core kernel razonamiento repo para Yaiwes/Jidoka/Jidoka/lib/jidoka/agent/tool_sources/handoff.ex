defmodule Jidoka.Agent.ToolSources.Handoff do
  @moduledoc false

  alias Jidoka.Agent.Dsl.Handoff
  alias Jidoka.Agent.ToolSources.Common
  alias Jidoka.Operation.Source
  alias Jidoka.Operation.Source.Handoff, as: HandoffSource
  alias Jidoka.Review.Approval

  @spec compiled!(term()) :: Jidoka.Operation.Source.Compiled.t()
  def compiled!(%Handoff{} = handoff) do
    source = source!(handoff)

    Common.compile_source!(source, handoff.approval, fn source, _operations ->
      metadata(source, handoff)
    end)
  end

  @spec source!(term()) :: HandoffSource.t()
  def source!(%Handoff{} = handoff) do
    HandoffSource.new!(
      agent: handoff.agent,
      as: handoff.as,
      description: handoff.description,
      target: handoff.target || :auto,
      forward_context: handoff.forward_context || :public,
      metadata: handoff.metadata || %{}
    )
  end

  @spec operations!(term()) :: [Jidoka.Agent.Spec.Operation.t()]
  def operations!(%Handoff{} = handoff) do
    handoff
    |> source!()
    |> Source.operations()
    |> case do
      {:ok, operations} -> Approval.apply_to_operations!(operations, handoff.approval)
      {:error, reason} -> raise ArgumentError, "invalid handoff source: #{inspect(reason)}"
    end
  end

  @spec metadata!(term()) :: [map()]
  def metadata!(%Handoff{} = handoff) do
    source = source!(handoff)
    metadata(source, handoff)
  end

  defp metadata(source, handoff) do
    [
      %{
        "source" => "handoff",
        "name" => source.name,
        "agent" => inspect(source.agent),
        "target" => inspect(source.target),
        "forward_context" => inspect(source.forward_context),
        "approval" => Approval.source_policy_map(handoff.approval)
      }
      |> Jidoka.Agent.ToolSources.Common.reject_nil_values()
    ]
  end
end
